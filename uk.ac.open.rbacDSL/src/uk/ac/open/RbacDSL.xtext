grammar uk.ac.open.RbacDSL with org.eclipse.xtext.common.Terminals

generate rbacDSL "http://www.ac.uk/open/RbacDSL"

Rbac:
	policies+=Policy*
	constraints+=Constraints*
;

Policy:
	'policy' name=ID '{'
	policyElements += PolicyElement*
	'}'
;

Constraints:
	'constraints' name=ID '{'
		constraints += PolicyConstraint*
	'}'
;

PolicyConstraint:
	GrantedConstraint|ForbiddenConstraint
;

ForbiddenConstraint:
	'forbidden' name=ID '{'
		'users' '{' users+= [User]+ '}'
		'roles' '{' roles += [Role]+ '}'
		'actions' '{' actions+=ObjectActions+ '}'
	'}'
;

GrantedConstraint:
	'granted' name=ID '{'
		'users' '{' users+= [User]+ '}'
		'roles' '{' roles += [Role]+ '}'
		'actions' '{' actions+=ObjectActions+ '}'
	'}'
;

PolicyElement:
	User|Role|Object|SoD
;

User:
	'user' name=ID '{'
		('roles' '{'roles += [Role]+ '}')?
	'}'
;

Role:
	'role' name=ID ('extends' parents += [Role]+)? '{'
		('permissions' '{' permissions += ObjectActions+ '}')?
	'}'
;

SoD:
	SSoD|DSoD
;

SSoD:
	'ssod' '{'
		(ssod += TupleRole)+
	'}'
;

DSoD:
	'dsod' '{'
		(dsod += TupleRole)+
	'}'
;

TupleRole:
	'(' fst=[Role] snd=[Role] ')'
;

ObjectActions:
	object = [Object] '{' actions += Action+ '}'
;

ActionsRefs:
	references += [Action]+
;

Object:
	'object' name=ID '{'
		('actions' '{' actions += Action+ '}')?
	'}'
;

Action:
	name=ID
;