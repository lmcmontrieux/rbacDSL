grammar uk.ac.open.RbacDSL with org.eclipse.xtext.common.Terminals

generate rbacDSL "http://www.ac.uk/open/RbacDSL"

Rbac:
	policies+=Policy*
	constraints+=Constraints*
;

Policy:
	'policy' name=ID '{'
	policyElements += PolicyElement*
	'}'
;

Constraints:
	'constraints' name=ID '{'
		constraints += PolicyConstraint*
	'}'
;

PolicyConstraint:
	GrantedConstraint|ForbiddenConstraint
;

ForbiddenConstraint:
	'forbidden' name=ID '{'
		'users' '{' users+= [User|QualifiedName]+ '}'
		'roles' '{' roles += [Role|QualifiedName]+ '}'
		'actions' '{' actions+=[Action|QualifiedName]+ '}'
	'}'
;

GrantedConstraint:
	'granted' name=ID '{'
		'users' '{' users+= [User|QualifiedName]+ '}'
		'roles' '{' roles += [Role|QualifiedName]+ '}'
		'actions' '{' actions+=[Action|QualifiedName]+ '}'
	'}'
;

PolicyElement:
	User|Role|Object|SoD
;

User:
	'user' name=ID '{'
		roles += [Role|QualifiedName]*
	'}'
;

Role:
	'role' name=ID ('extends' parents += [Role]+)? '{'
		permissions += [Action|QualifiedName]*
	'}'
;

SoD:
	SSoD|DSoD
;

SSoD:
	'ssod' '{'
		(ssod += TupleRole)+
	'}'
;

DSoD:
	'dsod' '{'
		(dsod += TupleRole)+
	'}'
;

TupleRole:
	'(' fst=[Role] snd=[Role] ')'
;

Object:
	'object' name=ID '{'
		(actions += Action*)
	'}'
;

Action:
	name=ID
;

QualifiedName: ID ('.' ID)*;