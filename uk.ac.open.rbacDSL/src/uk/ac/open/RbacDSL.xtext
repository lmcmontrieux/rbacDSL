grammar uk.ac.open.RbacDSL with org.eclipse.xtext.common.Terminals

generate rbacDSL "http://www.ac.uk/open/RbacDSL"

Rbac:
	'policy' name=ID '{'
	policyElements += PolicyElement*
	'}'
;

PolicyElement:
	User|Role|Object
;

User:
	'user' name=ID '{'
		('roles' roles += UserRoleAssignments)?
	'}'
;

UserRoleAssignments:
	'{'
	roles += [Role] (',' roles += [Role])*  
	'}'
;

Role:
	'role' name=ID '{'
		sod += SoD*
		(permissions += Permissions)?
	'}'
;

SoD:
	SSoD|DSoD
;

SSoD:
	'ssod' '{'
		ssod += [Role] (',' ssod += [Role])*
	'}'
;

DSoD:
	'dsod' '{'
		dsod += [Role] (',' dsod += [Role])*
	'}'
;

Permissions:
	'permissions' '{'
		permissions += ObjectActions (',' permissions += ObjectActions)*
	'}'
;

ObjectActions:
	object += [Object] '{' actions += [Action] (',' actions += [Action])* '}'
;

ActionsRefs:
	references += [Action] (',' references += [Action])*
;

Object:
	'object' name=ID '{'
		('actions' '{' actions += Action (',' actions += Action)* '}')?
	'}'
;

Action:
	name=ID
;