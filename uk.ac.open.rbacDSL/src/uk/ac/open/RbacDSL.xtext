grammar uk.ac.open.RbacDSL with org.eclipse.xtext.common.Terminals

generate rbacDSL "http://www.ac.uk/open/RbacDSL"

Rbac:
	'policy' '{'
		(policyElements += PolicyElement)*
	'}'
	'scenarios' '{'
		(scenarioElements += ScenarioElement)*
	'}'
;

PolicyElement:
	User | Role | Permission | RBACObject
;

ScenarioElement:
	GrantedScenario | ForbiddenScenario | UserRoleScenario | ObjectRoleScenario | ObjectScenario
;

User:
	'user' name = ID '{'
		('role' roles += [Role])*
	'}'
;

Role:
	'role' name = ID ('extends' parent += [Role]*)? '{'
		('ssod' ssod += [Role])*
		('dsod' dsod += [Role])*
		('object' assignments += Assignment)*
	'}'
;

Assignment:
	object = [RBACObject] '['(actions += [Permission])*']'
;

Permission:
	'action' name = ID
;

RBACObject:
	'object' name = ID '{'
		('action' permissions += [Permission])*
	'}'
;

GrantedScenario:
	'grantedScenario' name = ID '{'
		'user' user = [User]
		('role' roles += [Role])*
		('object' object += Assignment)+
	'}'
;

ForbiddenScenario:
	'forbiddenScenario' name = ID '{'
		'user' user = [User]
		('role' roles += [Role])*
		('object' object += Assignment)+
	'}'
;

UserRoleScenario:
	'userRoleScenario' name = ID '{'
		('role' roles += [Role])+
	'}'
;

ObjectRoleScenario:
	'objectRoleScenario' name = ID '{'
		'role' role += [Role]
		('object' assignment += Assignment)+
	'}'
;

ObjectScenario:
	'objectScenario' name = ID '{'
		('object' assignment += Assignment)+
	'}'
;