grammar uk.ac.open.RbacDSL with org.eclipse.xtext.common.Terminals

generate rbacDSL "http://www.ac.uk/open/RbacDSL"

Rbac:
	policies+=Policy*
	constraints+=Constraints*
;

Policy:
	'policy' name=ID '{'
	policyElements += PolicyElement*
	'}'
;

Constraints:
	'constraints' name=ID '{'
		constraints += PolicyConstraint*
	'}'
;

PolicyConstraint:
	UserConstraint|UserRoleConstraint
	|RoleOperationConstraint|OperationConstraint
;

UserConstraint:
	ForbiddenConstraint|GrantedConstraint
;

ForbiddenConstraint:
	'forbidden' name=ID '{'
		'users' '{' users+= [User|QualifiedName]* '}'
		'roles' '{' roles += [Role|QualifiedName]* '}'
		'operations' '{' operations+=[Operation|QualifiedName]* '}'
	'}'
;

GrantedConstraint:
	'granted' name=ID '{'
		'users' '{' users+= [User|QualifiedName]* '}'
		'roles' '{' roles += [Role|QualifiedName]* '}'
		'operations' '{' operations+=[Operation|QualifiedName]* '}'
	'}'
;

UserRoleConstraint:
	{UserRoleConstraint}'user-role' '{' roles += [Role|QualifiedName]* '}'
;

RoleOperationConstraint:
	'role-operations' name=ID '{'
		'role' role = [Role|QualifiedName]
		'operations' '{' operations += [Operation|QualifiedName]* '}'
	'}'
;

OperationConstraint:
	{OperationConstraint} 'operations' '{' operations += [Operation|QualifiedName]* '}'
;

PolicyElement:
	User|Role|Object|SoD
;

User:
	'user' name=ID '{'
		roles += [Role|QualifiedName]*
	'}'
;

Role:
	'role' name=ID ('extends' parents += [Role]+)? '{'
		permissions += [Operation|QualifiedName]*
	'}'
;

SoD:
	SSoD|DSoD
;

SSoD:
	{SSoD}'ssod' '{'
		ssod += TupleRole*
	'}'
;

DSoD:
	{DSoD}'dsod' '{'
		dsod += TupleRole*
	'}'
;

TupleRole:
	'(' fst=[Role] snd=[Role] ')'
;

Object:
	'object' name=ID '{'
		(operations += Operation*)
	'}'
;

Operation:
	name=ID
;

QualifiedName: ID ('.' ID)*;